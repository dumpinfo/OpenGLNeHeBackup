include get.e
include dll.e
include machine.e
include msgbox.e
include constants.ew
include GLfunc.ew
include GLconst.ew

without warning

atom hRC, hDC, hWnd, hInstance, ClassName
sequence keys keys = repeat(0,256)  -- array to hold key presses

integer active, fullscreen, retval
active = TRUE
fullscreen = TRUE
hRC = NULL
hDC = NULL
hWnd = NULL
hInstance = NULL

integer scroll, maxtokens, swidth, sheight, base
scroll = 0
maxtokens = 0

atom textures
textures = allocate(20)
integer imageData, bpp, width, height, texID
imageData = 0
bpp = 4
width = 8
height = 12
texID = 16

function fread(atom dest, integer size, integer src)
integer x
    for loop = 0 to size - 1 do
        x = getc(src)
        if x = -1 then
            mem_set(dest,0,12)
            return FALSE
        else
            poke(dest + loop, x)
        end if
    end for
    return size
end function

function memcmp(atom l1, atom l2, integer len)
    for loop = 0 to len - 1 do
        if peek(l1+loop) != peek(l2+loop) then
            return FALSE
        end if
    end for
    return TRUE
end function

function MakeString(atom StringAddress)
sequence string
integer i
string = {}
i = 0
    while 1 do
        if peek(StringAddress+i) = NULL then
            exit
        end if
        string = string & peek(StringAddress+i)
        i += 1
    end while
    return string
end function

function LoadTGA(atom texture, sequence Filename)
integer File, bytesPerPixel, imageSize, temp, gl_mode
atom filename, TGAheader, TGAcompare, header
    TGAheader= allocate(12)
    mem_set(TGAheader,0,12)
    poke(TGAheader+2,2)
    TGAcompare = allocate(12)
    header = allocate(6)
    filename = allocate_string(Filename)
    gl_mode = GL_RGBA

    File = open(Filename,"rb")
    if File = -1 or not fread(TGAcompare,12,File) or not memcmp(TGAheader,TGAcompare,12) or
                    not fread(header, 6, File) then
        if File = -1 then
            return FALSE
        else
            close(File)
            return FALSE
        end if
    end if

    poke4(texture+width,peek(header+1)*256+peek(header))
    poke4(texture+height,peek(header+3)*256+peek(header+2))

    if peek4u(texture+width) < 0 or peek4u(texture+width) = 0 or
       peek4u(texture+height) < 0 or peek4u(texture+height) = 0 or
       (peek(header+4) != 24 and peek(header+4) != 32) then
        close(File)
        return FALSE
    end if

    poke4(texture + bpp, peek(header + 4))
    bytesPerPixel = peek4u(texture+bpp)/8
    imageSize = peek4u(texture+width)*peek4u(texture+height)*bytesPerPixel

    poke4(texture+imageData,allocate(imageSize))

    if peek4u(texture+imageData) = NULL then
        close(File)
        return FALSE
    end if
    if fread(peek4u(texture+imageData),imageSize,File) != imageSize then
        close(File)
        return FALSE
    end if

    for loop = 0 to imageSize - 1 by bytesPerPixel do
        temp = peek(peek4u(texture+imageData)+loop)
        poke(peek4u(texture+imageData)+loop,peek(peek4u(texture+imageData)+loop+2))
        poke(peek4u(texture+imageData)+loop+2,temp)
    end for

    close(File)

    -- Build A Texture From The Data
    c_proc(glGenTextures,{1,texture+texID})

    c_proc(glBindTexture,{GL_TEXTURE_2D, peek4u(texture+texID)})
    c_proc(glTexParameterf,{GL_TEXTURE_2D,GL_TEXTURE_MIN_FILTER,GL_LINEAR})
    c_proc(glTexParameterf,{GL_TEXTURE_2D,GL_TEXTURE_MAG_FILTER,GL_LINEAR})

    if peek4u(texture+bpp) = 24 then
        gl_mode = GL_RGB
    end if

    c_proc(glTexImage2D,{GL_TEXTURE_2D,0,gl_mode,peek4u(texture+width),peek4u(texture+height),0,
                         gl_mode,GL_UNSIGNED_BYTE,peek4u(texture+imageData)})

    return TRUE
end function

procedure BuildFont()
atom cx, cy

    base = c_func(glGenLists,{256})
    c_proc(glBindTexture,{GL_TEXTURE_2D, peek4u(textures+texID)})

    for loop = 0 to 255 do
        cx=remainder(loop,16.0)/16.0                      -- X Position Of Current Character
        cy=floor(loop/16)/16                           -- Y Position Of Current Character

        c_proc(glNewList,{base+loop,GL_COMPILE})        -- Start Building A List
            c_proc(glBegin,{GL_QUADS})                  -- Use A Quad For Each Character
                c_proc(glTexCoord2f,{cx,1-cy-0.0625})   -- Texture Coord (Bottom Left)
                c_proc(glVertex2i,{0,16})                -- Vertex Coord (Bottom Left)
                c_proc(glTexCoord2f,{cx+0.0625,1-cy-0.0625})    -- Texture Coord (Bottom Right)
                c_proc(glVertex2i,{16,16})               -- Vertex Coord (Bottom Right)
                c_proc(glTexCoord2f,{cx+0.0625,1-cy})   -- Texture Coord (Top Right)
                c_proc(glVertex2i,{16,0})              -- Vertex Coord (Top Right)
                c_proc(glTexCoord2f,{cx,1-cy})          -- Texture Coord (Top Left)
                c_proc(glVertex2i,{0,0})                -- Vertex Coord (Top Left)
            c_proc(glEnd,{})                            -- Done Building Our Quad (Character)
            c_proc(glTranslated,{14,0,0})               -- Move To The Right Of The Character
        c_proc(glEndList,{})
    end for
end procedure

procedure KillFont()
    c_proc(glDeleteLists,{base,256})
end procedure

procedure glPrint(atom x, atom y, sequence text, integer set)
atom text_loc
    if set > 1 then
        set = 1
    end if
    text_loc = allocate_string(text)
    c_proc(glEnable,{GL_TEXTURE_2D})
    c_proc(glLoadIdentity,{})
    c_proc(glTranslated,{x,y,0})
    c_proc(glListBase,{base-32+(128*set)})
    c_proc(glScalef,{1.0,2.0,1.0})
    c_proc(glCallLists,{length(text),GL_UNSIGNED_BYTE,text_loc})
    c_proc(glDisable,{GL_TEXTURE_2D})
    free(text_loc)
end procedure

procedure ReSizeGLScene(integer width, integer height)
    sheight = height
    swidth = width
    if height = 0 then
        height = 1
    end if
    c_proc(glViewport,{0,0,width,height})
    c_proc(glMatrixMode,{GL_PROJECTION})
    c_proc(glLoadIdentity,{})
    c_proc(glOrtho,{0.0,640,480,0.0,-1.0,1.0})
    c_proc(glMatrixMode,{GL_MODELVIEW})
    c_proc(glLoadIdentity,{})
end procedure

procedure InitGL()
    if not LoadTGA(textures,"Data\\Font.TGA") then
        puts(1,"LoadTGA failed!" & '\n')
    end if

    BuildFont()

    c_proc(glShadeModel,{GL_SMOOTH})
    c_proc(glClearColor,{0.0,0.0,0.0,0.5})
    c_proc(glClearDepth,{1.0})
    c_proc(glBindTexture,{GL_TEXTURE_2D,peek4u(textures+texID)})

end procedure

function DrawGLScene()
sequence token, text
integer cnt
    token = {}
    cnt = 0

    c_proc(glClear, {or_bits(GL_COLOR_BUFFER_BIT,GL_DEPTH_BUFFER_BIT)})
    c_proc(glColor3f,{1.0,0.5,0.5})
    glPrint(50,16,"Renderer",1)
    glPrint(80,48,"Vendor",1)
    glPrint(66,80,"Version",1)

    c_proc(glColor3f,{1.0,0.7,0.4})

    glPrint(200,16,MakeString(c_func(glGetString,{GL_RENDERER})),1)
    glPrint(200,48,MakeString(c_func(glGetString,{GL_VENDOR})),1)
    glPrint(200,80,MakeString(c_func(glGetString,{GL_VERSION})),1)

    c_proc(glColor3f,{0.5,0.5,1.0})
    glPrint(192,432,"NeHe Productions",1)

    c_proc(glLoadIdentity,{})
    c_proc(glColor3f,{1.0,1.0,1.0})
    c_proc(glBegin,{GL_LINE_STRIP})
        c_proc(glVertex2d,{639,417})
        c_proc(glVertex2d,{  0,417})
        c_proc(glVertex2d,{  0,480})
        c_proc(glVertex2d,{639,480})
        c_proc(glVertex2d,{639,128})
    c_proc(glEnd,{})
    c_proc(glBegin,{GL_LINE_STRIP})
        c_proc(glVertex2d,{  0,128})
        c_proc(glVertex2d,{639,128})
        c_proc(glVertex2d,{639,  1})
        c_proc(glVertex2d,{  0,  1})
        c_proc(glVertex2d,{  0,417})
    c_proc(glEnd,{})

    c_proc(glScissor,{1,floor(0.135416*sheight),swidth-2,floor(0.597916*sheight)})
    c_proc(glEnable,{GL_SCISSOR_TEST})

    text = MakeString(c_func(glGetString,{GL_EXTENSIONS}))
    for loop = 1 to length(text) do
        if cnt > maxtokens then
            maxtokens = cnt
        end if
        if text[loop] != ' ' then
            token = token & text[loop]
        else
            cnt += 1
            c_proc(glColor3f,{0.5,1.0,0.5})
            glPrint(1,97+(cnt*32)-scroll,sprintf("%d",cnt),0)
            c_proc(glColor3f,{1.0,1.0,0.5})
            glPrint(50,97+(cnt*32)-scroll,token,0)
            token={}
        end if
        if loop = length(text) then
            cnt += 1
            c_proc(glColor3f,{0.5,1.0,0.5})
            glPrint(1,97+(cnt*32)-scroll,sprintf("%d",cnt),0)
            c_proc(glColor3f,{1.0,1.0,0.5})
            glPrint(50,97+(cnt*32)-scroll,token,0)
            token={}
        end if
    end for

    c_proc(glDisable,{GL_SCISSOR_TEST})

    c_proc(glFlush,{})

    return TRUE
end function

integer dmScreenSettings, WindowRect

procedure KillGLWindow()
    if fullscreen then
        if c_func(ChangeDisplaySettingsA,{NULL,0}) then end if
        if c_func(ShowCursor,{TRUE}) then end if
    end if
    if hRC then
        if c_func(wglMakeCurrent,{NULL,NULL}) then end if
        if c_func(wglDeleteContext,{hRC}) then end if
        hRC = NULL
    end if
    if hRC and not c_func(ReleaseDC,{hWnd,hDC}) then
        hDC = NULL
    end if
    if hWnd and not c_func(DestroyWindow,{hWnd}) then
        hWnd = NULL
    end if
    if dmScreenSettings then
        free(dmScreenSettings)
    end if
    KillFont()
    free(WindowRect)
end procedure

function WndProc(atom hWnd, integer uMsg, atom wParam, atom lParam)
    if uMsg = WM_ACTIVATE then
        if not floor(wParam/#10000) then
            active = TRUE
        else
            active = FALSE
        end if
    elsif  uMsg = WM_SYSCOMMAND then
        if wParam = SC_SCREENSAVE then end if
        if wParam = SC_MONITORPOWER then end if
    elsif uMsg = WM_CLOSE then
        c_proc(PostQuitMessage,{0})
    elsif uMsg = WM_KEYDOWN then
        keys[wParam] = TRUE
    elsif uMsg = WM_KEYUP then
        keys[wParam] = FALSE
    elsif uMsg = WM_SIZE then
        ReSizeGLScene(and_bits(lParam,#FFFF),floor(lParam/#10000))
    end if
    return c_func(DefWindowProcA,{hWnd, uMsg, wParam, lParam})
end function

integer wc wc = allocate(40)
function ClassRegistration()
integer WndProcAddress, id
    id = routine_id("WndProc")
    if id = -1 then
    puts(1, "routine_id failed!\n")
    abort(1)
    end if
    WndProcAddress = call_back(id)
    hInstance = c_func(GetModuleHandleA,{NULL})
    ClassName = allocate_string("OpenGL")
    poke4(wc,or_all({CS_HREDRAW, CS_VREDRAW, CS_OWNDC}))
    poke4(wc+4,WndProcAddress)
    poke4(wc+8,0)
    poke4(wc+12,0)
    poke4(wc+16,hInstance)
    poke4(wc+20,c_func(LoadIconA,{NULL,IDI_WINLOGO}))
    poke4(wc+24,c_func(LoadCursorA,{NULL, IDC_ARROW}))
    poke4(wc+28,NULL)
    poke4(wc+32,NULL)
    poke4(wc+36,ClassName)
    if not c_func(RegisterClassA,{wc}) then
        retval = message_box("Failed to register class","Error", or_bits(MB_OK,MB_ICONINFORMATION))
        return FALSE
    else
        return TRUE
    end if
end function

integer regd regd = FALSE
procedure CreateGLWindow(atom title, integer width, integer height, integer bits, integer fullscreenflag)
    atom PixelFormat, pfd, dwExStyle, dwStyle
    sequence s
    if regd = FALSE then
        if ClassRegistration() then
            regd = TRUE
        end if
    end if
    fullscreen = fullscreenflag
    if fullscreen then
        dmScreenSettings = allocate(156)
        mem_set(dmScreenSettings,0,156)
        s = int_to_bytes(156)
        poke(dmScreenSettings + 36,{s[1],s[2]})
        poke4(dmScreenSettings + 40,or_all({DM_BITSPERPEL,DM_PELSWIDTH,DM_PELSHEIGHT}))
        poke4(dmScreenSettings + 104, bits)
        poke4(dmScreenSettings + 108, width)
        poke4(dmScreenSettings + 112, height)
        if c_func(ChangeDisplaySettingsA,{dmScreenSettings,CDS_FULLSCREEN}) != DISP_CHANGE_SUCCESSFUL then
            if message_box("The requested fullscreen mode is not supported by\nyour video card. " &
                           "Use windowed mode instead?","Error", or_bits(MB_YESNO,MB_ICONEXCLAMATION)) = IDYES then
            else
                retval = message_box("Program will now close","Error",or_bits(MB_OK,MB_ICONSTOP))
            end if
        end if
    else
        dmScreenSettings = NULL
    end if
    if fullscreen then
        dwExStyle = WS_EX_APPWINDOW
        dwStyle = WS_POPUP
        if c_func(ShowCursor,{FALSE}) then end if
    else
        dwExStyle = or_bits(WS_EX_APPWINDOW,WS_EX_WINDOWEDGE)
        dwStyle = WS_OVERLAPPEDWINDOW
    end if
    WindowRect = allocate(16)
    poke4(WindowRect,0)
    poke4(WindowRect + 4,width)
    poke4(WindowRect + 8, 0)
    poke4(WindowRect + 12, height)
    if c_func(AdjustWindowRectEx,{WindowRect, dwStyle, FALSE, dwExStyle}) then end if
    hWnd = c_func(CreateWindowExA,{dwExStyle,  --extended window style
                                   ClassName,  --class
                                   title,      --window caption
                                   or_all({WS_CLIPSIBLINGS,WS_CLIPCHILDREN,dwStyle}),  --window style
                                   0,
                                   0,
                                   peek4u(WindowRect + 4) - peek4u(WindowRect),
                                   peek4u(WindowRect + 12) - peek4u(WindowRect + 8),
                                   NULL,
                                   NULL,
                                   hInstance,
                                   NULL})
    if hWnd = NULL then
        KillGLWindow()
        retval = message_box("Window creation error","Error",or_bits(MB_OK,MB_ICONEXCLAMATION))
    end if
    pfd = allocate(40)  --PIXELFORMATDESCRIPTOR
    mem_set(pfd,0,40)
    poke(pfd, 40)  --size of pfd structure
    poke(pfd + 2, 1) --version
    poke4(pfd + 4, or_all({PFD_DRAW_TO_WINDOW,PFD_SUPPORT_OPENGL,PFD_DOUBLEBUFFER})) --properties flags
    poke(pfd + 8, PFD_TYPE_RGBA)  --request an rgba format
    poke(pfd + 9, 24)  --select color depth
    poke(pfd + 23, 24)  --32bit Z-buffer

    hDC = c_func(GetDC,{hWnd})  --create GL device context to match window device context
    if not hDC then
        KillGLWindow()
        retval = message_box("Can't create a GL device context","Error",or_bits(MB_OK,MB_ICONEXCLAMATION))
    end if
    PixelFormat = c_func(ChoosePixelFormat,{hDC,pfd})  --find a pixel format matching PIXELFORMATDESCRIPTOR
    if not PixelFormat then
        KillGLWindow()
        retval = message_box("Can't find a suitable pixel format","Error",or_bits(MB_OK,MB_ICONEXCLAMATION))
    end if
    if not (c_func(SetPixelFormat,{hDC,PixelFormat,pfd})) then  --set the pixel format
        KillGLWindow()
        retval = message_box("Can't set the pixel format","Error",or_bits(MB_OK,MB_ICONEXCLAMATION))
    end if
    if not c_func(DescribePixelFormat, {hDC,PixelFormat,40,pfd}) then
        retval = message_box("Can't describe the pixel format","Error",or_bits(MB_OK,MB_ICONEXCLAMATION))
    end if
    hRC = c_func(wglCreateContext,{hDC})  --create GL rendering context
    if not hRC then
        KillGLWindow()
        retval = message_box("Can't create a GL rendering context","Error",or_bits(MB_OK,MB_ICONEXCLAMATION))
    end if
    if not (c_func(wglMakeCurrent,{hDC,hRC})) then  --make the GL rendering context active
        KillGLWindow()
        retval = message_box("Can't activate the GL rendering context","Error",or_bits(MB_OK,MB_ICONEXCLAMATION))
    end if
    retval = c_func(ShowWindow,{hWnd,SW_SHOW}) --show the window
    retval = c_func(SetForegroundWindow,{hWnd}) --set it to always be in foreground
    retval = c_func(SetFocus,{hWnd}) --give it focus
    ReSizeGLScene(width-26, height-28)  --draw the GL scene to match the window size
    InitGL()  --initialize OpenGL
end procedure

integer MSG MSG = allocate(28)
integer title title = allocate_string("OpenGL")
procedure WinMain()
integer done, msg_message
    done = FALSE
    if message_box("Would you like to run in fullscreen mode?","Start Fullscreen?",or_bits(MB_YESNO,MB_ICONQUESTION)) = IDNO then
        fullscreen = FALSE
    else
        fullscreen = TRUE
    end if
    CreateGLWindow(title,640,480,24,fullscreen)
    while not done do
        if c_func(PeekMessageA,{MSG,NULL,0,0,PM_REMOVE}) then
            msg_message = peek4u(MSG+4)
            if msg_message = WM_QUIT then
                done = TRUE
            else
                retval = c_func(TranslateMessage,{MSG})
                retval = c_func(DispatchMessageA,{MSG})
            end if
        else
            if ((active and not DrawGLScene()) or keys[VK_ESCAPE]) then
                done = TRUE
            else
                retval = c_func(SwapBuffers,{hDC})
                if keys[VK_F1] then
                    keys[VK_F1] = FALSE
                    KillGLWindow()
                    if fullscreen = 0 then
                        fullscreen = 1
                    else
                        fullscreen = 0
                    end if
                    CreateGLWindow(title,640,480,24,fullscreen)
                end if
                if keys[VK_UP] and scroll > 0 then
                    scroll -= 2
                end if
                if keys[VK_DOWN] and scroll < 32*(maxtokens-9) then
                    scroll += 2
                end if
            end if
        end if
    end while
    KillGLWindow()
end procedure

WinMain()